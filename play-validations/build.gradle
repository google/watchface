/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
//        classpath 'com.guardsquare:proguard-gradle:7.1.0'
    }
}

plugins {
    id "com.diffplug.spotless" version "6.25.0" apply false
}

allprojects { project ->
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    // Ignore third party code
    if (project.name != 'validator') {
        apply plugin: 'com.diffplug.spotless'
        spotless {
            java {
                target("src/**/*.java")
                googleJavaFormat().aosp().reflowLongStrings()
                removeUnusedImports()
            }
        }
    }
}

def libModules = List.of("validator", "memory-footprint")
subprojects { project ->
    if (project.name in libModules) {
        afterEvaluate {
            version = "1.6.0-SNAPSHOT"
            group = "com.google.watchface"

            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        from components.java
                    }
                }

                repositories {
                    mavenLocal()
                    if (System.getenv("GITHUB_PACKAGES_URL") != null) {
                        maven {
                            name = "GithubPackages"
                            url = System.getenv("GITHUB_PACKAGES_URL")
                            credentials {
                                username = System.getenv("GITHUB_PACKAGES_USER")
                                password = System.getenv("GITHUB_PACKAGES_TOKEN")
                            }
                        }
                    }
                }
            }
        }
    }
}
