/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id("com.android.application")
}

android {
    namespace = "com.google.wear.watchface.memory.sample"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.google.wear.watchface.memory.sample"
        minSdk = 33
        targetSdk = 33
        versionCode = 1
        versionName = "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = false
            signingConfig = signingConfigs.getByName("debug")
        }
    }
}

afterEvaluate {
    // create a directory containing an unpacked DWF bundle, used for tests
    tasks.register('unpackBundle', Copy) {
        into layout.buildDirectory.dir("outputs/unpackedBundle/release")

        from(layout.projectDirectory.dir("src/main/res")) {
            into("base/res")
        }

        from(layout.projectDirectory.dir("src/main/AndroidManifest.xml")) {
            into("base/manifest")
        }
    }

    // create a directory containing only a res sub-directory, used for tests
    tasks.register("resDirectory", Copy) {
        into layout.buildDirectory.dir("outputs/resDirectory/release")

        from(layout.projectDirectory.dir("src/main/res")) {
            into("res")
        }
    }

    // create a zip that follows the structure used internally, for tests
    tasks.register('zipApk', Zip) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        dependsOn 'assembleDebug'
        from 'build/outputs/apk/debug'
        include 'sample-wf-debug.apk'
        dependsOn 'assembleRelease'
        from 'build/outputs/apk/release'
        include 'sample-wf-release.apk'
        rename {
            'com.google.wear.watchface.memory.sample-base-split.apk'
        }
        archiveBaseName = "com.google.wear.watchface.memory.sample"
        destinationDirectory = layout.buildDirectory.dir("outputs/zipApk")
    }
}
